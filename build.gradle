
plugins {
    id 'io.wusa.semver-git-plugin'
}


description = 'Evolution of github.com:hypfvieh/dbus-java.git [gradle, java 11, windows compat]'

// version = semver.info

allprojects { Project prj ->
    prj.group = 'com.gpaglia.dbus'
    prj.version = '0.1.0-SNAPSHOT'  // default if no semver
    prj.getPlugins().withId('io.wusa.semver-git-plugin') {
        //noinspection GroovyAssignabilityCheck
        // version from git
        prj.version = semver.info.toString()
        prj.semver {
            branches {
                branch {
                    regex = ".+"
                    incrementer = "MINOR_INCREMENTER"
                    formatter = {
                        "${semver.info.version.major}.${semver.info.version.minor}.${semver.info.version.patch}+build.${semver.info.count}.sha.${semver.info.shortCommit}"
                    }
                }
            }
        }
    }
}

subprojects { Project subp ->
    subp.getPlugins().withId('java') {

        // set java version and other standards for projects with java plugin installed
        subp.java {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
            if (! subp.getName().endsWith('osgi')) {
                withJavadocJar()
                withSourcesJar()
                // modularity enabled
                modularity.inferModulePath = true
            }
        }

        // modularity enabled
        // define a java9 sourceSet
        subp.sourceSets {
            //noinspection GroovyAssignabilityCheck
            main {
                java {
                    srcDir 'src/main/java9'
                }
            }
        }

        subp.tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs += [
                    // jdk8 parameter names
                    '-parameters',
                    // '-proc:none',
                    '-Xlint:deprecation'
            ]

            // modularity disabled, not working
            // use the project's version or define one directly
            // options.javaModuleVersion = provider { project.version }
        }

        subp.tasks.withType(Test) {
            useJUnitPlatform {
                // options here
            }
        }

        subp.tasks.withType(Javadoc) {
            options.encoding = 'UTF-8'
        }

        subp.afterEvaluate { Project p ->
            p.task(type: Jar, 'testJar') {
                group = 'build'
                //noinspection GroovyAssignabilityCheck
                archiveClassifier = 'tests'
                from p.sourceSets.test.output
            }

            // p.jar.dependsOn testJar

            if (! subp.getName().endsWith('osgi')) {
                artifacts {
                    archives sourcesJar, javadocJar, testJar
                }
            }

            // publishing
            p.publishing {
                publications.create("publish-${p.name}", MavenPublication) {
                    artifact jar
                    if (! p.getName().endsWith('osgi')) {
                        artifact sourcesJar
                        artifact javadocJar
                        artifact testJar
                    }
                }

                repositories {
                    mavenLocal()
                }
            }
        }
    }

    /* modularity disabled, not working
    subp.getPlugins().withId('java-test-fixtures') { it ->
        subp.sourceSets {
            //noinspection GroovyAssignabilityCheck
            testFixtures {
                java {
                    srcDir 'src/main/java9'
                }
            }
        }
    }
     */

    // configure repositories
    subp.repositories {
        mavenLocal()
        jcenter()
    }

}

